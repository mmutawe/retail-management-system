plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

java {
    sourceCompatibility = '17'
}

def retailBusinessProjects = [
        project('product-service'),
        project('order-service'),
        project('inventory-service')
]

// allprojects = subprojects (children) + parent project
//allprojects {
//subprojects {
configure (retailBusinessProjects) {
    println "*** start gradle.build for \'${project.name}\' project."

    apply plugin:'java'
    apply plugin:'org.springframework.boot'
    apply plugin:'io.spring.dependency-management'

    group 'com.mmutawe.projects.ms.retail'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudVersion', "2022.0.3")
    }

    dependencies {
        // web
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // jpa and db
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'org.postgresql:postgresql'

        // webclient
        implementation 'org.springframework.boot:spring-boot-starter-webflux'

        // cloud
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    
    tasks.named('test') {
        useJUnitPlatform()
    }
}